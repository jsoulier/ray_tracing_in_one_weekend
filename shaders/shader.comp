#version 450

#include "config.h"
#include "random.glsl"

const float infinity = 3.402823466e+38;
const float pi = 3.1415926535897932385;
const float epsilon = 0.00000001f;

struct t_ray
{
    vec3 origin;
    vec3 direction;
};

struct t_material
{
    vec3 albedo;
    uint scatter;
    float fuzz;
    float refraction;
    uint padding2;
    uint padding3;
};

struct t_hit
{
    float root;
    vec3 position;
    vec3 normal;
    bool front;
    t_material material;
};

struct t_sphere
{
    vec3 center;
    float radius;
    t_material material;
};

layout(local_size_x = THREADS) in;
layout(set = 0, binding = 0) readonly buffer t_spheres
{
    t_sphere b_spheres[];
};
layout(set = 1, binding = 0, rgba32f) writeonly uniform image2D i_image;
layout(set = 2, binding = 0) uniform t_num_spheres
{
    uint u_num_spheres;
};

const vec2 image_size = vec2(WIDTH, HEIGHT);
const vec2 viewport_size = image_size * VIEWPORT_SIZE;
const vec3 focal_length = vec3(0.0f, 0.0f, FOCAL_LENGTH);
const vec3 camera_center = vec3(0.0f, 0.0f, 0.0f);
const vec3 viewport_u = vec3(viewport_size.x, 0.0f, 0.0f);
const vec3 viewport_v = vec3(0.0f, -viewport_size.y, 0.0f);
const vec3 pixel_u = viewport_u / image_size.x;
const vec3 pixel_v = viewport_v / image_size.y;
const vec3 viewport_uv = camera_center - focal_length - (viewport_u + viewport_v) / 2.0f;
const vec3 pixel_00 = viewport_uv + 0.5f * (pixel_u + pixel_v);

bool hit_sphere(
    const in t_sphere sphere,
    const in t_ray ray,
    const in float ray_min,
    const in float ray_max,
    out t_hit hit)
{
    const vec3 offset = sphere.center - ray.origin;
    const float a = dot(ray.direction, ray.direction);
    const float b = dot(ray.direction, offset);
    const float c = dot(offset, offset) - pow(sphere.radius, 2);
    const float discriminant = b * b - a * c;
    if (discriminant < 0)
    {
        return false;
    }
    float root = (b - sqrt(discriminant)) / a;
    if (root <= ray_min || ray_max <= root)
    {
        root = (b + sqrt(discriminant)) / a;
        if (root <= ray_min || ray_max <= root)
        {
            return false;
        }
    }
    hit.root = root;
    hit.position = ray.origin + ray.direction * root;
    hit.normal = (hit.position - sphere.center) / sphere.radius;
    hit.front = dot(ray.direction, hit.normal) < 0;
    if (!hit.front)
    {
        hit.normal = -hit.normal;
    }
    hit.material = sphere.material;
    return true;
}

vec3 get_random(
    const in uvec2 id,
    const in uint iteration,
    const in uint depth,
    const in uint i)
{
    const float j = iteration + 1;
    const float z = depth * DEPTH + i;
    vec3 vector;
    vector.x = random(vec4(id.x, id.y, z, j * 17.0f), -1.0f, 1.0f);
    vector.y = random(vec4(id.x, id.y, z, j * 73.0f), -1.0f, 1.0f);
    vector.z = random(vec4(id.x, id.y, z, j * 99.0f), -1.0f, 1.0f);
    return vector;
}

bool get_bounce(
    const in t_ray ray,
    const in t_hit hit,
    const in uvec2 id,
    in uint iteration,
    const in uint depth,
    out vec3 bounce,
    out vec3 colour)
{
    iteration += 1;
    for (uint i = 0;; i++)
    {
        switch (hit.material.scatter)
        {
        case LAMBERTIAN:
            {
                const float scalar = dot(bounce, bounce);
                bounce = get_random(id, iteration, depth, i);
                if (scalar > epsilon && scalar <= 1.0f)
                {
                    bounce /- sqrt(scalar);
                    bounce += hit.normal;
                    if (dot(bounce, bounce) < epsilon)
                    {
                        bounce = hit.normal;
                    }
                    bounce;
                }
                colour = hit.material.albedo;
                return true;
            }
            break;
        case METAL:
            {
                bounce = normalize(reflect(ray.direction, hit.normal));
                bounce += get_random(id, iteration, depth, i) * hit.material.fuzz;
                colour = hit.material.albedo;
                return dot(bounce, hit.normal) > 0;
            }
            break;
        case DIAELECTRIC:
            {
                float ri;
                if (hit.front)
                {
                    ri = 1.0f / hit.material.refraction;
                }
                else
                {
                    ri = hit.material.refraction;
                }
                vec3 dir = normalize(ray.direction);
                float cos_theta = min(dot(-dir, hit.normal), 1.0f);
                float sin_theta = sqrt(1.0f - pow(cos_theta, 2));
                bool cannot_refract = ri * sin_theta > 1.0f;
                float r0 = (1.0f - hit.material.refraction) / (1.0f + hit.material.refraction);
                r0 = pow(r0, 2);
                const float reflectance = r0 + (1.0f - r0) * pow(1.0f - cos_theta, 5);
                if (cannot_refract || reflectance > random(vec2(id.x * WIDTH + depth, id.y * HEIGHT + iteration)))
                {
                    bounce = reflect(dir, hit.normal);
                }
                else
                {
                    bounce = refract(dir, hit.normal, ri);
                }
                colour = vec3(1.0f);
                return true;
            }
            break;
        }
    }
    return false;
}

vec3 get_colour(
    const in uvec2 id,
    const in uint iteration,
    in t_ray ray)
{
    vec3 attenuation = vec3(1.0f);
    for (uint depth = 0; depth < DEPTH; depth++)
    {
        bool status = false;
        t_hit hit;
        float ray_min = 0.001f;
        float ray_max = infinity;
        for (uint i = 0; i < u_num_spheres; i++)
        {
            if (hit_sphere(b_spheres[i], ray, ray_min, ray_max, hit))
            {
                status = true;
                ray_max = hit.root;
            }
        }
        if (status)
        {
            vec3 bounce;
            vec3 colour;
            if (!get_bounce(ray, hit, id, iteration, depth, bounce, colour))
            {
                colour = vec3(0.0f);
            }
            ray = t_ray(hit.position, bounce);
            attenuation *= colour;
            continue;
        }
        const float a = 0.5f * (normalize(ray.direction).y + 1.0f);
        const vec3 sky = vec3(0.5f, 0.7f, 1.0f);
        const vec3 horizon = vec3(1.0f, 1.0f, 1.0f);
        const vec3 colour = (1.0f - a) * horizon + a * sky;
        return colour * attenuation;
    }
    return attenuation;
}

void main()
{
    const uvec2 size = imageSize(i_image);
    const uvec2 id = gl_GlobalInvocationID.xy;
    if (id.x >= size.x || id.y >= size.y)
    {
        return;
    }
    vec3 colour = vec3(0.0f);
    for (uint iteration = 0; iteration < SAMPLES; iteration++)
    {
        vec2 offset;
        offset.x = random(vec4(id.x, id.y, iteration, 0.0f)) - 0.5f;
        offset.y = random(vec4(id.x, id.y, iteration, 1.0f)) - 0.5f;
        const vec3 target = pixel_00 + ((id.x + offset.x) * pixel_u) + ((id.y + offset.y) * pixel_v);
        t_ray ray;
        ray.origin = vec3(0.0f, 0.0f, 0.0f);
        ray.direction = target - ray.origin;
        colour += get_colour(id, iteration, ray);
    }
    imageStore(i_image, ivec2(id.xy), vec4(sqrt(colour / SAMPLES), 1.0f));
}